<program> :: = <statement-list>

<statement-list> ::= <statement> <statement-list>
                    | <statement>

<statement> ::= "Show" (<expression> | <text> )
              | "Let" <identifier> "be" <type>
              | "Set" <identifier> "to" <expression>
              | "Increase" <identifier> "by" <expression>
              | "Decrease" <identifier> "by" <expression>
              | "Multiply" <identifier> "by" <expression>
              | "Divide" <identifier> "by" <expression>
              | "If" <comparison> "then:" <nl> <program>
              | "Repeat" <factor> "times:" <nl> <program>

<comparison> ::= [ "not" ] <simple-comparison> { ("and" | "or") <comparison> }

<simple-comparison> ::= <expression> <comparison-operator> <expression>

<expression> ::= <term> { ( "add" | "subtract" ) <term> }

<term> ::= <factor> { ( "times" | "divide" ) <factor> }

<factor> ::= [ "+" | "-" ] ( <number> | <identifier> )

<type> ::= "integer"
         | "decimal"
         | "boolean"

<comparison-operator> ::= "equalsTo"
                        | "notEqualsTo"
                        | "greaterThan"
                        | "lessThan"
                        | "atLeast"
                        | "atMost"

<text> ::= '"' { any-character-except-quote } '"'

<nl> ::= '\n'+